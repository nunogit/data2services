# Retrieve all sources (graph)
# /explore
# explore
PREFIX skos: <http://www.w3.org/2004/02/skos/core#>
PREFIX dcat: <http://www.w3.org/ns/dcat#>
PREFIX dctypes: <http://purl.org/dc/dcmitype/>
PREFIX dct: <http://purl.org/dc/terms/>
PREFIX foaf: <http://xmlns.com/foaf/0.1/>
PREFIX idot: <http://identifiers.org/idot/>
PREFIX rdf: <http://www.w3.org/1999/02/22-rdf-syntax-ns#>
PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>
PREFIX void: <http://rdfs.org/ns/void#>
PREFIX bl: <http://w3id.org/biolink/vocab/>
PREFIX sio: <http://semanticscience.org/resource/>
SELECT ?source
WHERE {
        ?dataset a dctypes:Dataset ;
            idot:preferredPrefix ?source .
        ?version dct:isVersionOf ?dataset ; 
            dcat:distribution [ a void:Dataset ; dcat:accessURL ?graph ] . 
}

# Retrieving all classes from a source
# /{source}
# query
PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>
PREFIX dct: <http://purl.org/dc/terms/>
PREFIX bl: <http://w3id.org/biolink/vocab/>
PREFIX dctypes: <http://purl.org/dc/dcmitype/>
PREFIX idot: <http://identifiers.org/idot/>
PREFIX dcat: <http://www.w3.org/ns/dcat#>
PREFIX void: <http://rdfs.org/ns/void#>
PREFIX sio: <http://semanticscience.org/resource/>
SELECT ?source ?class ?classLabel ?count
WHERE
{
    {
        SELECT ?source ?class (count(?class) as ?count)  
        WHERE {
            ?dataset a dctypes:Dataset ; idot:preferredPrefix ?source .
            ?version dct:isVersionOf ?dataset ; dcat:distribution [ a void:Dataset ; dcat:accessURL ?graph ] . 
            FILTER(?source = "?_source")
            graph ?graph {
                [] a ?class .
            }
        }
        group by ?source ?class
        order by desc(?count)
    }
    optional {
        ?class rdfs:label ?classLabel .
    }
}

# Retrieving the list of entities corresponding to requested class
# /{source}/{class}
# query
PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>
PREFIX dct: <http://purl.org/dc/terms/>
PREFIX bl: <http://w3id.org/biolink/vocab/>
PREFIX dctypes: <http://purl.org/dc/dcmitype/>
PREFIX idot: <http://identifiers.org/idot/>
PREFIX dcat: <http://www.w3.org/ns/dcat#>
PREFIX void: <http://rdfs.org/ns/void#>
PREFIX sio: <http://semanticscience.org/resource/>
SELECT ?source ?class ?entity
WHERE 
{   
    ?dataset a dctypes:Dataset ; idot:preferredPrefix ?source .
    ?version dct:isVersionOf ?dataset ; dcat:distribution [ a void:Dataset ; dcat:accessURL ?graph ] . 
    FILTER(?source = "?_source")
    GRAPH ?graph 
    {
        ?entityUri a ?class .
        ?entityUri a ?_class .
        ?entityUri bl:id ?entity
    }
}

# Retrieving the properties of the requested instance of a class, filtering the class on its ID
# /{source}/{class}/{id}
# query
PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>
PREFIX dct: <http://purl.org/dc/terms/>
PREFIX bl: <http://w3id.org/biolink/vocab/>
PREFIX dctypes: <http://purl.org/dc/dcmitype/>
PREFIX idot: <http://identifiers.org/idot/>
PREFIX dcat: <http://www.w3.org/ns/dcat#>
PREFIX void: <http://rdfs.org/ns/void#>
PREFIX sio: <http://semanticscience.org/resource/>
SELECT ?source ?class ?entity ?property ?value
WHERE
{
    ?dataset a dctypes:Dataset ; idot:preferredPrefix ?source .
    ?version dct:isVersionOf ?dataset ; dcat:distribution [ a void:Dataset ; dcat:accessURL ?graph ] . 
    FILTER(?source = "?_source")
    GRAPH ?graph
    {
        ?entityUri a ?_class .
        ?entityUri a ?class .
        ?entityUri bl:id ?entity .
        ?entityUri ?property ?value .
        FILTER(?entity = "?_id")
    }
}

# Retrieving the value of specific property of the requested instance of a class, filtering the class on its ID
# /{source}/{class}/{id}/{property}
# query
PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>
PREFIX dct: <http://purl.org/dc/terms/>
PREFIX bl: <http://w3id.org/biolink/vocab/>
PREFIX dctypes: <http://purl.org/dc/dcmitype/>
PREFIX idot: <http://identifiers.org/idot/>
PREFIX dcat: <http://www.w3.org/ns/dcat#>
PREFIX void: <http://rdfs.org/ns/void#>
PREFIX sio: <http://semanticscience.org/resource/>
SELECT ?source ?class ?entity ?property ?value
WHERE
{
    ?dataset a dctypes:Dataset ; idot:preferredPrefix ?source .
    ?version dct:isVersionOf ?dataset ; dcat:distribution [ a void:Dataset ; dcat:accessURL ?graph ] . 
    FILTER(?source = "?_source")
    GRAPH ?graph
    {
        ?entityUri a ?_class .
        ?entityUri a ?class .
        ?entityUri bl:id ?entity .
        ?entityUri ?property ?value .
        FILTER(?entity = "?_id")
        FILTER(?property = ?_property)
    }
}